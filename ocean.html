<!DOCTYPE html>
<html lang="en">

<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <script src="./node_modules/@tweenjs/tween.js/dist/tween.umd.js"></script>
    <title>Document</title>
    <style>
        body {
            font-family: sans-serif;
            font-size: 11px;
            background-color: rgb(148, 146, 146);
            margin: 0px;
            position: relative;
        }

        canvas {
            display: block;
        }

        .title {
            position: absolute;
            font-size: 30px;
            font-weight: bold;
            color: #f9f9f9;
            left: 50%;
            top: 40%;
            transform: translate(-50%, -50%);
            text-shadow: 5px 5px 5px rgba(0, 0, 0,0.6);

        }

        .title p {
            line-height: 50px;
            text-align: center;
        }

        .title p:first-child {
            font-size: 60px;
            letter-spacing: 15px;
        }
    </style>
</head>

<body>
    <div class="title">
        <p>我们会航向怎样的未来?</p>
        <!-- <p> 无数命运流转</p>
        <p> 打造了无数的相异的罗盘</p> -->
    </div>
    <script id="vertexShader" type="x-shader/x-vertex">
        void main() {
          vec4 mvPosition = modelViewMatrix * vec4( position, 1.0 );
          gl_Position = projectionMatrix * mvPosition;
        }
        </script>

    <script id="fragmentShader" type="x-shader/x-fragment">

        // 时间
        uniform float iTime;
        // 分辨率
        uniform vec2 iResolution;
        // 鼠标位置
        uniform vec2 iMouse;

		const int NUM_STEPS = 8;
        const float PI      = 3.141592;
        const float EPSILON = 1e-3;
        #define EPSILON_NRM (0.1 / iResolution.x)

        // sea
        const int ITER_GEOMETRY = 3;
        const int ITER_FRAGMENT = 5;
        const float SEA_HEIGHT = 0.6;
        const float SEA_CHOPPY = 4.0;
        const float SEA_SPEED = 0.8;
        const float SEA_FREQ = 0.16;
        const vec3 SEA_BASE = vec3(0.1,0.19,0.22);
        const vec3 SEA_WATER_COLOR = vec3(0.8,0.9,0.6);
        #define SEA_TIME (1.0 + iTime * SEA_SPEED)
        const mat2 octave_m = mat2(1.6,1.2,-1.2,1.6);

        // math
        mat3 fromEuler(vec3 ang) {
            vec2 a1 = vec2(sin(ang.x),cos(ang.x));
            vec2 a2 = vec2(sin(ang.y),cos(ang.y));
            vec2 a3 = vec2(sin(ang.z),cos(ang.z));
            mat3 m;
            m[0] = vec3(a1.y*a3.y+a1.x*a2.x*a3.x,a1.y*a2.x*a3.x+a3.y*a1.x,-a2.y*a3.x);
            m[1] = vec3(-a2.y*a1.x,a1.y*a2.y,a2.x);
            m[2] = vec3(a3.y*a1.x*a2.x+a1.y*a3.x,a1.x*a3.x-a1.y*a3.y*a2.x,a2.y*a3.y);
            return m;
        }
        float hash( vec2 p ) {
            float h = dot(p,vec2(127.1,311.7)); 
            return fract(sin(h)*43758.5453123);
        }
        float noise( in vec2 p ) {
            vec2 i = floor( p );
            vec2 f = fract( p );    
            vec2 u = f*f*(3.0-2.0*f);
            return -1.0+2.0*mix( mix( hash( i + vec2(0.0,0.0) ), 
                            hash( i + vec2(1.0,0.0) ), u.x),
                        mix( hash( i + vec2(0.0,1.0) ), 
                            hash( i + vec2(1.0,1.0) ), u.x), u.y);
        }

        // lighting
        float diffuse(vec3 n,vec3 l,float p) {
            return pow(dot(n,l) * 0.4 + 0.6,p);
        }
        float specular(vec3 n,vec3 l,vec3 e,float s) {    
            float nrm = (s + 8.0) / (PI * 8.0);
            return pow(max(dot(reflect(e,n),l),0.0),s) * nrm;
        }

        // sky
        vec3 getSkyColor(vec3 e) {
            e.y = max(e.y,0.0);
            return vec3(pow(1.0-e.y,2.0), 1.0-e.y, 0.6+(1.0-e.y)*0.4);
        }

        // sea
        float sea_octave(vec2 uv, float choppy) {
            uv += noise(uv);        
            vec2 wv = 1.0-abs(sin(uv));
            vec2 swv = abs(cos(uv));    
            wv = mix(wv,swv,wv);
            return pow(1.0-pow(wv.x * wv.y,0.65),choppy);
        }

        float map(vec3 p) {
            float freq = SEA_FREQ;
            float amp = SEA_HEIGHT;
            float choppy = SEA_CHOPPY;
            vec2 uv = p.xz; uv.x *= 0.75;

            float d, h = 0.0;    
            for(int i = 0; i < ITER_GEOMETRY; i++) {        
                d = sea_octave((uv+SEA_TIME)*freq,choppy);
                d += sea_octave((uv-SEA_TIME)*freq,choppy);
                h += d * amp;        
                uv *= octave_m; freq *= 1.9; amp *= 0.22;
                choppy = mix(choppy,1.0,0.2);
            }
            return p.y - h;
        }

        float map_detailed(vec3 p) {
            float freq = SEA_FREQ;
            float amp = SEA_HEIGHT;
            float choppy = SEA_CHOPPY;
            vec2 uv = p.xz; uv.x *= 0.75;

            float d, h = 0.0;    
            for(int i = 0; i < ITER_FRAGMENT; i++) {        
                d = sea_octave((uv+SEA_TIME)*freq,choppy);
                d += sea_octave((uv-SEA_TIME)*freq,choppy);
                h += d * amp;        
                uv *= octave_m; freq *= 1.9; amp *= 0.22;
                choppy = mix(choppy,1.0,0.2);
            }
            return p.y - h;
        }

        vec3 getSeaColor(vec3 p, vec3 n, vec3 l, vec3 eye, vec3 dist) {  
            float fresnel = clamp(1.0 - dot(n,-eye), 0.0, 1.0);
            fresnel = pow(fresnel,3.0) * 0.65;

            vec3 reflected = getSkyColor(reflect(eye,n));    
            vec3 refracted = SEA_BASE + diffuse(n,l,80.0) * SEA_WATER_COLOR * 0.12; 

            vec3 color = mix(refracted,reflected,fresnel);

            float atten = max(1.0 - dot(dist,dist) * 0.001, 0.0);
            color += SEA_WATER_COLOR * (p.y - SEA_HEIGHT) * 0.18 * atten;

            color += vec3(specular(n,l,eye,60.0));

            return color;
        }

        // tracing
        vec3 getNormal(vec3 p, float eps) {
            vec3 n;
            n.y = map_detailed(p);    
            n.x = map_detailed(vec3(p.x+eps,p.y,p.z)) - n.y;
            n.z = map_detailed(vec3(p.x,p.y,p.z+eps)) - n.y;
            n.y = eps;
            return normalize(n);
        }

        float heightMapTracing(vec3 ori, vec3 dir, out vec3 p) {  
            float tm = 0.0;
            float tx = 1000.0;    
            float hx = map(ori + dir * tx);
            if(hx > 0.0) return tx;   
            float hm = map(ori + dir * tm);    
            float tmid = 0.0;
            for(int i = 0; i < NUM_STEPS; i++) {
                tmid = mix(tm,tx, hm/(hm-hx));                   
                p = ori + dir * tmid;                   
                float hmid = map(p);
                if(hmid < 0.0) {
                    tx = tmid;
                    hx = hmid;
                } else {
                    tm = tmid;
                    hm = hmid;
                }
            }
            return tmid;
        }

        // main
        void main() {
            vec2 uv = gl_FragCoord.xy / iResolution.xy;
            uv = uv * 2.0 - 1.0;
            uv.x *= iResolution.x / iResolution.y;    
            float time = iTime * 0.1 + iMouse.x * 0.0001;

            // ray
            vec3 ang = vec3(sin(time*3.0)*0.1,sin(time)*0.2+0.3,time);    
            vec3 ori = vec3(0.0,3.5,time*5.0);
            vec3 dir = normalize(vec3(uv.xy,-2.0)); dir.z += length(uv) * 0.15;
            dir = normalize(dir) * fromEuler(ang);

            // tracing
            vec3 p;
            heightMapTracing(ori,dir,p);
            vec3 dist = p - ori;
            vec3 n = getNormal(p, dot(dist,dist) * EPSILON_NRM);
            vec3 light = normalize(vec3(0.0,1.0,0.8)); 

            // color
            vec3 color = mix(
                getSkyColor(dir),
                getSeaColor(p,n,light,dir,dist),
                pow(smoothstep(0.0,-0.05,dir.y),0.3));

            // post
            gl_FragColor  = vec4(pow(color,vec3(0.75)), 1.0);
        }
    </script>

    <script type="module">
        import * as THREE from './node_modules/three/build/three.module.js';
        import { CSS2DRenderer, CSS2DObject } from './node_modules/three/examples/jsm/renderers/CSS2DRenderer.js';
        import { OrbitControls } from './node_modules/three/examples/jsm/controls/OrbitControls.js'
        import { APP } from './js/app.js';
        import { VRButton } from './js/VRButton.js';

        window.THREE = THREE; // Used by APP Scripts.
        window.VRButton = VRButton; // Used by APP Scripts.
        var player = new APP.Player();

        console.log(player)

        player.setScene(new THREE.Scene())
        player.setLight(new THREE.AmbientLight(0x353535, 3))
        player.setCamera(new THREE.PerspectiveCamera(45, window.innerWidth / window.innerHeight, 1, 1000))
        // player.camera.up = new THREE.Vector3( 0, 0, 1 )
        // player.setControls(new OrbitControls(player.camera, player.dom))
        player.camera.position.set(50, 50, 50)
        player.setSize(window.innerWidth, window.innerHeight)

        window.addEventListener('resize', function () {
            player.setSize(window.innerWidth, window.innerHeight);
            player.setCSS2DRender(new CSS2DRenderer(), player.dom)
            player.camera.aspect = window.innerWidth / window.innerHeight;
            player.camera.updateProjectionMatrix();
        });

        // var geometry = new THREE.BoxBufferGeometry(2, 2, 2);
        // var material = new THREE.MeshBasicMaterial({ color: 0x00ff00 });
        // var cube = new THREE.Mesh(geometry, material);

        // player.scene.add(cube);

        function createdOcean() {
            let uniforms = {
                iTime: { value: 1.0 },
                iResolution: { value: new THREE.Vector2(window.innerWidth * 1.0, window.innerHeight * 1.0) },
                iMouse: { value: new THREE.Vector2(0.0, 0.0) }
            }
            let material = new THREE.ShaderMaterial({
                uniforms: uniforms,
                vertexShader: document.getElementById('vertexShader').innerHTML,
                fragmentShader: document.getElementById('fragmentShader').innerHTML
            });
            let geometry = new THREE.PlaneBufferGeometry(window.innerWidth, window.innerHeight);
            let ocean = new THREE.Mesh(geometry, material);
            ocean.name = 'ocean'
            player.scene.add(ocean);

            window.addEventListener("mousemove", function (event) {
                if (!mouseStartPosition) {
                    mouseStartPosition = { x: event.clientX, y: event.clientY }
                } else {
                    uniforms.iMouse.value.x = event.clientX - mouseStartPosition.x;
                    uniforms.iMouse.value.y = event.clientY - mouseStartPosition.y;
                }
            })
        }

        createdOcean()

        var mouseStartPosition = null;

        player.play()

        document.body.appendChild(player.dom)

    </script>
</body>

</html>